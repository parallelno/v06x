#version 120

uniform sampler2D Texture0;
#define color_texture Texture0
#define TEXCOORD gl_TexCoord[0].st

uniform vec2 color_texture_sz;
uniform vec2 screen_texture_sz;

uniform float filter_gain;
uniform float filter_invgain;

#define PI          3.14159265358
#define FSC         4433618.75
#define LINETIME    64.0e-6 // 64 us total
#define VISIBLE     52.0e-6 // 52 us visible part
#define FLINE       (1.0/LINETIME) // =15625 for 64ms, but = 19230 accounting for visible part only
#define VISIBLELINES 288//312

#define RGB_to_YIQ  mat3x3( 0.299 , 0.595716 , 0.211456 ,   0.587    , -0.274453 , -0.522591 ,      0.114    , -0.321263 , 0.311135 )
#define YIQ_to_RGB  mat3x3( 1.0   , 1.0      , 1.0      ,   0.9563   , -0.2721   , -1.1070   ,      0.6210   , -0.6474   , 1.7046   )

#define RGB_to_YUV  mat3x3( 0.299 , -0.14713 , 0.615    ,   0.587    , -0.28886  , -0.514991 ,      0.114    , 0.436     , -0.10001 )
#define YUV_to_RGB  mat3x3( 1.0   , 1.0      , 1.0      ,   0.0      , -0.39465  , 2.03211   ,      1.13983  , -0.58060  , 0.0      )

#define fetch(ofs,center,invx) texture2D(color_texture, vec2((ofs) * (invx) + center.x, center.y))

#define FIRTAPS 20
//const float FIR[FIRTAPS] = float[FIRTAPS] (-0.008030271,0.003107906,0.016841352,0.032545161,0.049360136,0.066256720,0.082120150,0.095848433,0.106453014,0.113151423,0.115441842,0.113151423,0.106453014,0.095848433,0.082120150,0.066256720,0.049360136,0.032545161,0.016841352,0.003107906);


const float FIR[20] = float[20] (0.032510684078502015,0.037230050756041834,0.041843026634912281,0.046224816069039297,0.050252793446409441,0.053811167664048955,0.056795464455619823,0.059116625356802809,0.060704537603613343,0.061510833935010188,0.061510833935010188,0.060704537603613343,0.059116625356802809,0.056795464455619823,0.053811167664048955,0.050252793446409441,0.046224816069039297,0.041843026634912281,0.037230050756041834,0.032510684078502015);

#define FIR_GAIN filter_gain
#define FIR_INVGAIN filter_invgain

float width_ratio;
float height_ratio;
float altv;
float invx;

// CLAMP seems to cause too much aliasing that I can't fix, so NO_CLAMP 
#define NOCLAMP
#define CLAMP_LIMIT 0.8

float luma_only(vec2 xy)
{
    vec3 rgb = fetch(0, xy, invx).xyz;
    vec3 yuv = RGB_to_YUV * rgb;
    return yuv.x;
}

float modulated(vec2 xy, float sinwt, float coswt) {
    vec3 rgb = fetch(0, xy, invx).xyz;
    vec3 yuv = RGB_to_YUV * rgb;

#ifdef CLAMP
    return clamp(yuv.x + yuv.y * sinwt + yuv.z * coswt, -CLAMP_LIMIT,CLAMP_LIMIT);
#else
    return yuv.x + yuv.y * sinwt + yuv.z * coswt;
#endif
}

vec2 modem_uv(vec2 xy, int ofs) {
    float t = (xy.x + ofs * invx) * color_texture_sz.x;
    float wt = t * 2 * PI / width_ratio;

    float sinwt = sin(wt);
    float coswt = cos(wt + altv);

    vec3 rgb = fetch(ofs, xy, invx).xyz;
    vec3 yuv = RGB_to_YUV * rgb;
#ifdef CLAMP
    float signal = clamp(yuv.x + yuv.y * sinwt + yuv.z * coswt, -CLAMP_LIMIT,CLAMP_LIMIT);
#else
    float signal = yuv.x + yuv.y * sinwt + yuv.z * coswt;
#endif
    return vec2(signal * sinwt, signal * coswt);
}

// Scanline divider depends on how the screen is scanned in the machine
// Machines that simply display 2 equal fields, like Atari 8-bit
// need this value to be 1.0 for correct scanlines, because they have
// 2 TV lines per horizontal pixel.
// Unfortunately, this has no chance of looking decent on medium
// resolution LCD with less than 1000 lines to display.
// Decent looking fake effect can be achieved by using non-integer values.
#define DIV_ATARI 2.0
#define DIV_MSX 4.0
#define SCANLINE_DIV 3
#define VFREQ PI*(screen_texture_sz.y)/SCANLINE_DIV
// scanline offset relative to pixel boundary
#define VPHASEDEG 90
#define VPHASE (VPHASEDEG)*PI/(180.0*VFREQ)
// difference between scanline max and min intensities
#define PROMINENCE 0.2
// 1.0 makes lines with maximal luma fuse together
#define FLATNESS 1.0

float scanline(float y, float luma) {
    // scanlines
    float w = (y + VPHASE) * VFREQ;
    
    float flatness = 2.0 - luma * 2.0 * FLATNESS;  // more luminance = more flatness
    float sinw = pow(abs(sin(w)), flatness);
    sinw = (1.0 - PROMINENCE) + sinw * PROMINENCE;    

    return sinw;
}


void main(void) {
    vec2 xy = TEXCOORD;

    width_ratio = color_texture_sz.x / (FSC / FLINE);
    height_ratio = color_texture_sz.y / VISIBLELINES;

    altv = mod(floor(xy.y * VISIBLELINES + 0.5), 2.0) * PI;
    invx = 0.25 / (FSC/FLINE); // equals 4 samples per Fsc period

    // lowpass U/V at baseband
    vec2 filtered = vec2(0.0, 0.0);
    for (int i = 0; i < FIRTAPS; i++) {
        vec2 uv = modem_uv(xy, i - FIRTAPS/2);
        filtered += FIR_GAIN * uv * FIR[i];
    }

    float t = xy.x * color_texture_sz.x;
    float wt = t * 2 * PI / width_ratio;

    float sinwt = sin(wt);
    float coswt = cos(wt + altv);

    //float luma = modulated(xy, sinwt, coswt) - FIR_INVGAIN * (filtered.x * sinwt + filtered.y * coswt);
    float luma = luma_only(xy);
    vec3 yuv_result = scanline(xy.y, luma) * vec3(luma, filtered.x, filtered.y);

    gl_FragColor = vec4(YUV_to_RGB * yuv_result, 1.0);
}
